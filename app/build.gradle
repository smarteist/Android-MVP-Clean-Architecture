apply plugin: 'com.android.application'

android {


    compileSdkVersion 28

    defaultConfig {
        applicationId "com.smarteist.mrnews"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        debug {
            minifyEnabled false
            // Uses new built-in shrinker http://tools.android.com/tech-docs/new-build-system/built-in-shrinker
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }

        release {
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }
    }

    buildTypes.each {
        it.buildConfigField 'String', 'NewsAPIKEY', NewsApiKey
    }

    flavorDimensions "default"

    productFlavors {
        mock {
            dimension "default"
            applicationIdSuffix = ".mock"
        }
        prod {
            dimension "default"
        }
    }

    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if (variant.buildType.name == 'release'
                && variant.getFlavors().get(0).name == 'mock') {
            variant.setIgnore(true)
        }
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
    buildToolsVersion '28.0.3'
}

/*
 Dependency versions are defined in the top level build.gradle file. This helps keeping track of
 all versions in a single place. This improves readability and helps managing project complexity.
 */
dependencies {

// Define versions in a single place
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    ext {

        // App dependencies
        supportLibraryVersion = '28.0.0'
        supportCardviewVersion = '28.0.0'
        supportCustomTabsVersion = '28.0.0'
        constraintLayout = '1.1.3'
        guavaVersion = '18.0'
        junitVersion = '4.12'
        mockitoVersion = '1.10.19'
        powerMockito = '1.6.2'
        hamcrestVersion = '1.3'
        runnerVersion = '1.0.1'
        rulesVersion = '1.0.1'
        espressoVersion = '3.0.2'
        roomVersion = "1.1.1"
        googleFindBugs = "3.0.2"
        daggerVersion = '2.21'
        dexmakerVersion = '1.2'
        rxAndroidVersion = '2.0.2'
        rxJavaVersion = '2.1.7'
        retrofitClientVersion = '2.3.0'
        retrofitAdapterRxJavaVersion = '2.3.0'
        gsonVersion = '2.8.2'
        gsonConverterVersion = '2.3.0'
        picassoVersion = '2.5.2'
    }

    // App's dependencies, including test
    implementation "com.android.support:support-v4:$supportLibraryVersion"
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    implementation "com.android.support.constraint:constraint-layout:$constraintLayout"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
    implementation "com.android.support:support-v4:$supportLibraryVersion"
    implementation "com.android.support.test.espresso:espresso-idling-resource:$espressoVersion"
    implementation "com.google.guava:guava:$guavaVersion"
    implementation "android.arch.persistence.room:runtime:$roomVersion"
    annotationProcessor "android.arch.persistence.room:compiler:$roomVersion"

    // Dagger dependencies
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    compileOnly 'org.glassfish:javax.annotation:10.0-b28'
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-android-processor:$daggerVersion"

    // Dependencies for local unit tests
    testImplementation "junit:junit:$ext.junitVersion"
    testImplementation "org.mockito:mockito-all:$ext.mockitoVersion"
    testImplementation "org.hamcrest:hamcrest-all:$ext.hamcrestVersion"

    // Android Testing Support Library's runner and rules
    androidTestImplementation "com.android.support.test:runner:$ext.runnerVersion"
    androidTestImplementation "com.android.support.test:rules:$ext.rulesVersion"

    androidTestImplementation "android.arch.persistence.room:testing:$roomVersion"

    // Dependencies for Android unit tests
    androidTestImplementation "junit:junit:$ext.junitVersion"
    androidTestImplementation "org.mockito:mockito-core:$ext.mockitoVersion"
    androidTestImplementation "com.google.dexmaker:dexmaker:$ext.dexmakerVersion"
    androidTestImplementation "com.google.dexmaker:dexmaker-mockito:$ext.dexmakerVersion"

    // Espresso UI Testing
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$espressoVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$espressoVersion"
    androidTestImplementation "com.android.support.test.espresso.idling:idling-concurrent:$espressoVersion"
    implementation "com.android.support.test.espresso:espresso-idling-resource:$espressoVersion"

    // Resolve conflicts between main and test APK:
    androidTestImplementation "com.android.support:support-annotations:$supportLibraryVersion"
    androidTestImplementation "com.android.support:support-v4:$supportLibraryVersion"
    androidTestImplementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
    androidTestImplementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    androidTestImplementation "com.android.support:design:$supportLibraryVersion"
    androidTestImplementation "com.google.code.findbugs:jsr305:$googleFindBugs"

    //RxJava and rxAndroid, retrofit,  picasso and custom tabs dependencies
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    implementation "com.squareup.retrofit2:retrofit:$retrofitClientVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitAdapterRxJavaVersion"
    implementation "com.google.code.gson:gson:$gsonVersion"
    implementation "com.squareup.retrofit2:converter-gson:$gsonConverterVersion"
    implementation "com.squareup.picasso:picasso:$picassoVersion"
    implementation "com.android.support:customtabs:$supportCustomTabsVersion"
//    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
}
repositories {
    mavenCentral()
}
